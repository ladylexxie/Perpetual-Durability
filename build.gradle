plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
}

architectury { minecraft = rootProject.mc_version }

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.mc_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.mod_id
    version = rootProject.mod_version
    group = "ladylexxie.${mod_id}"

    repositories {
        maven {
            url = "https://maven.architectury.dev/"
            content { includeGroup "me.shedaniel" }
        }
        maven {
            url = "https://maven.saps.dev/releases"
            content {
                includeGroup "dev.latvian.mods"
                includeGroup "dev.ftb.mods"
            }
        }
        maven { url = "https://maven.blamejared.com/" }     // JEI
        maven { url = "https://maven.shedaniel.me/" }       // REI / Cloth Config
        maven { url = "https://maven.parchmentmc.net/" }    // parchment
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java { withSourcesJar() }
}
