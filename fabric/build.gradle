plugins { id "com.github.johnrengelman.shadow" version "7.1.2" }

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modApi "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}+${rootProject.mc_version}"
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${rootProject.rei_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rootProject.rei_version}")
    modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}")

    modCompileOnly("mezz.jei:jei-${rootProject.mc_version}-fabric:${rootProject.jei_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }
}

processResources {
    def toReplace = [
            "version"             : project.version,
            "mc_version"          : rootProject.mc_version,
            "rei_version"         : rootProject.rei_version,
            "jei_version"         : rootProject.jei_version,
            "architectury_version": rootProject.architectury_version,
            "fabric_api_version"  : rootProject.fabric_api_version,
            "cloth_config_version": rootProject.cloth_config_version
    ]

    inputs.properties toReplace
    filesMatching("fabric.mod.json") {
        expand toReplace
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    setArchiveClassifier("dev-shadow")
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveBaseName.set "perpetual_durability-${mc_version}-${mod_version}-fabric"
    archiveClassifier.set null
}

jar { setArchiveClassifier("dev") }

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
