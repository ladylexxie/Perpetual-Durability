plugins { id "com.github.johnrengelman.shadow" version "7.1.2" }

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.mc_version}-${rootProject.forge_version}"
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"
    modApi("me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}")
    modCompileOnly("mezz.jei:jei-${rootProject.mc_version}-forge:${rootProject.jei_version}") { transitive = false }
    modLocalRuntime modCompileOnly("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    def toReplace = [
            "version"             : project.version,
            "mc_version"          : rootProject.mc_version,
            "rei_version"         : rootProject.rei_version,
            "jei_version"         : rootProject.jei_version,
            "architectury_version": rootProject.architectury_version,
            "cloth_config_version": rootProject.cloth_config_version
    ]

    inputs.properties toReplace
    filesMatching("META-INF/mods.toml") {
        expand toReplace
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    setArchiveClassifier("dev-shadow")
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveBaseName.set "perpetual_durability-${mc_version}-${mod_version}-forge"
    archiveClassifier.set null
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "perpetual_durability",
                "Specification-Vendor"    : "LadyLexxie",
                "Specification-Version"   : project.version,
                "Implementation-Title"    : "Perpetual Durability",
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "LadyLexxie",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    setArchiveClassifier("dev")
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
